// Sintaxes
<rowswritten> ::= KEY_LBRACE <commands> KEY_RBRACE;
<emptybracket> ::= KEY_LBRACKET #20 KEY_RBRACKET #21;
<emptybrace> ::= KEY_LBRACE KEY_RBRACE;
<typeofvalue> ::= KEY_VARIABLE #22 KEY_COLON <type>;

<deffunction> ::= KEY_FUNCTION #30 KEY_VARIABLE #23 KEY_LPAREN KEY_RPAREN KEY_COLON
  | KEY_FUNCTION #30 KEY_VARIABLE #23 KEY_LPAREN <params> KEY_RPAREN KEY_COLON;
  
<increment> ::= KEY_VARIABLE #24 KEY_INCREMENT;
<arrayincrement> ::= KEY_VARIABLE #42 KEY_LBRACKET <exp> KEY_RBRACKET KEY_INCREMENT;
<decrement> ::= KEY_VARIABLE #24 KEY_DECREMENT;
<arraydecrement> ::= KEY_VARIABLE #42 KEY_LBRACKET <exp> KEY_RBRACKET KEY_DECREMENT;
<whilescope> ::= KEY_WHILE #36 KEY_LPAREN <exp> KEY_RPAREN;
<fortype> ::= KEY_TYPE_FLOAT | KEY_TYPE_INT; 
<forvalue> ::= KEY_INTEGER #40 | KEY_DECIMAL #40 | <indexedvalue> | <arraybyindexscope>; 
<forparams> ::= KEY_VARIABLE #38 KEY_COLON <fortype> #39 KEY_ATTR <forvalue> KEY_SEMICOLON <exp> KEY_SEMICOLON <exp> KEY_RPAREN
  | KEY_VARIABLE #38 KEY_ATTR <forvalue> KEY_SEMICOLON <exp> KEY_SEMICOLON <exp> KEY_RPAREN;
<forscope> ::= KEY_FOR #37 KEY_LPAREN KEY_VAR <forparams> | KEY_FOR #37 KEY_LPAREN <forparams>;
<attrstate> ::= KEY_CONST #25 | KEY_VAR #25;
<multiattrscope> ::= KEY_VARIABLE #26 KEY_COMMA <multiattrscope> | KEY_VARIABLE #26 KEY_COMMA <typeofvalue>;
<attrscope> ::= KEY_VARIABLE #22 | <attrstate> <typeofvalue> | <attrstate> <multiattrscope> | <indexedvalue>;      

<arrayvalues> ::= <exp> KEY_COMMA <arrayvalues> | <exp>;
<arrayscope> ::= <emptybracket> | KEY_LBRACKET <arrayvalues> KEY_RBRACKET;
<elifscope> ::= KEY_ELIF #34 KEY_LPAREN <exp> KEY_RPAREN;
<ifscope> ::= KEY_IF #34 KEY_LPAREN <exp> KEY_RPAREN;

<printscope> ::= KEY_LPAREN <exp> KEY_RPAREN KEY_SEMICOLON #17 | KEY_LPAREN <arrayscope> #17 KEY_RPAREN KEY_SEMICOLON;
<readscope> ::= KEY_LPAREN KEY_VARIABLE #18 KEY_RPAREN KEY_SEMICOLON
  | KEY_LPAREN <indexedvalue> #18 KEY_RPAREN KEY_SEMICOLON;

// Types
<primitivetype> ::= KEY_TYPE_INT | KEY_TYPE_FLOAT | KEY_TYPE_STRING | KEY_TYPE_BOOLEAN | KEY_TYPE_VOID;
<complextype> ::= <primitivetype> #19 <emptybracket>;
<type> ::= <primitivetype> #19 | <complextype>;

// Parameters
<param> ::= <typeofvalue> | <typeofvalue> KEY_COMMA <param>;
<params> ::= <param> <params> | <param>;
<attrsparams> ::= <exp> KEY_COMMA <attrsparams> | <exp>;

// Function
<function> ::= <deffunction> <type> <emptybrace> | <deffunction> <type> <rowswritten>;

// Loops
<while> ::= <whilescope> <statement>;
<dowhile> ::= KEY_DO #35 <statement> <whilescope> KEY_SEMICOLON;
<for> ::= <forscope> <statement>; 

// Attribution
<attrvalue> ::= <exp> KEY_COMMA <attrvalue> | <exp> KEY_SEMICOLON;
<attrarray> ::= <arrayscope> KEY_COMMA <attrarray> | <array>;
<arraybyindexscope> ::= <arrayscope> KEY_LBRACKET #28 <exp> KEY_RBRACKET #29;
<attrarraybyindex> ::= <arraybyindexscope> KEY_COMMA <attrarraybyindex> | <arraybyindexscope> KEY_SEMICOLON;

<attr> ::= <attrscope> KEY_ATTR <attrvalue>
  | <attrscope> KEY_ATTR <attrarray>
  | <attrscope> KEY_ATTR <attrarraybyindex>
  | <attrscope> KEY_SEMICOLON
  | <increment> #27 KEY_SEMICOLON 
  | <arrayincrement> KEY_SEMICOLON 
  | <decrement> #27 KEY_SEMICOLON
  | <arraydecrement> KEY_SEMICOLON;

// Conditionals
<switchcase> ::= KEY_SWITCH #41 KEY_LPAREN <exp> KEY_RPAREN KEY_LBRACE <cases> KEY_RBRACE 
  | KEY_SWITCH #41 KEY_LPAREN <exp> KEY_RPAREN KEY_LBRACE <cases> <default> KEY_RBRACE;
<cases> ::= <case> <cases> | <case>;
<case> ::= KEY_CASE #41 <exp> KEY_COLON <commands>;
<default> ::= KEY_DEFAULT #41 KEY_COLON <commands>;

<else> ::= KEY_ELSE #34 <statement>;
<elif> ::= <elifscope> <statement> | <elifscope> <statement> <else>;
<if> ::= <ifscope> <statement> | <ifscope> <statement> <else> | <ifscope> <statement> <elif>;

// Array
<array> ::= <arrayscope> KEY_SEMICOLON;

// I/O
<print> ::= KEY_PRINT <printscope>;
<read> ::= KEY_READ <readscope>;

// Comments
<comments> ::= KEY_COMMENT_LINE #16 | KEY_COMMENT_MULT_LINE #16;

// Finals
<return> ::= KEY_RETURN #31 <exp> KEY_SEMICOLON
  | KEY_RETURN #31 KEY_SEMICOLON;
<break> ::= KEY_BREAK #32 KEY_SEMICOLON;
<throw> ::= KEY_THROW #33 <exp> KEY_SEMICOLON;

// Commands
<funccall> ::= KEY_VARIABLE #14 KEY_LPAREN KEY_RPAREN
  | KEY_VARIABLE #14 KEY_LPAREN <attrsparams> KEY_RPAREN;

<functioncall> ::= <funccall> KEY_SEMICOLON;

<statement> ::= <rowswritten> | <single_command>;
<single_command> ::= <print> | <read> | <attr> | <return> | <break> | <throw>;
<program> ::= <commands>;
<commands> ::= <command> <commands> | <command>;
<command> ::= <single_command> | <function> | <switchcase> | <comments> | <if> | <while> | <for> | <dowhile> | <functioncall>;
  
// Expressions
<exp>  ::= <exp> KEY_OR #2 <exp1> | <exp1>;
<exp1> ::= <exp1> KEY_AND #3 <exp2> | <exp2>;
<exp2> ::= <exp2> KEY_BIT_OR #4 <exp3> | <exp3>;
<exp3> ::= <exp3> KEY_EXPO #5 <exp4> | <exp4>;
<exp4> ::= <exp4> KEY_BIT_AND #6 <exp5> | <exp5>;
<exp5> ::= <exp5> <op_rel> #7 <exp6> | <exp6>;
<exp6> ::= <exp6> <op_bitwise> #8 <exp7> | <exp7>;
<exp7> ::= <exp7> <op_arit_baixa> #9 <exp8> | <exp8>;
<exp8> ::= <exp8> <op_arit_alta> #10 <exp9> | <exp9> ;
<exp9> ::= <op_neg> #11 <value> #1 | <value> #1;
<exp10> ::= <increment> | <decrement>;
<exp11> ::= <arrayincrement> | <arraydecrement>;

// Values
<value> ::= KEY_VARIABLE
  | KEY_DECIMAL
  | KEY_INTEGER
  | KEY_BINARY
  | KEY_HEXADECIMAL
  | KEY_STRING
  | KEY_TRUE
  | KEY_FALSE
  | KEY_LPAREN #12 <exp> KEY_RPAREN #13
  | <exp10>
  | <exp11>
  | <funccall>
  | <indexedvalue>;

<indexedvalue> ::= KEY_VARIABLE #42 KEY_LBRACKET <exp> KEY_RBRACKET;

// Operations
<op_rel> ::= KEY_GREATER | KEY_LESSER | KEY_EQUAL | KEY_GREATER_EQUAL | KEY_LESSER_EQUAL | KEY_ATTR | KEY_NOT_EQUAL; 
<op_neg> ::= KEY_SUB | KEY_BIT_NOT | KEY_NOT;
<op_bitwise> ::= KEY_BIT_SL | KEY_BIT_SR | KEY_BIT_XOR | KEY_BIT_NOT;
<op_arit_baixa> ::= KEY_SUM | KEY_SUB;
<op_arit_alta> ::= KEY_MULT | KEY_DIV | KEY_MOD;
